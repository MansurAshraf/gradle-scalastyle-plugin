/*
 *    Copyright 2014. Binh Nguyen
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

version = projectVersion
group = projectGroup

// Define projects
def twoTenProject = project(':gradle-scalastyle-plugin_2.10')
def twoElevenProject = project(':gradle-scalastyle-plugin_2.11')

twoTenProject.ext.scalaDist = "2.10"
twoElevenProject.ext.scalaDist = "2.11"

task wrapper(type: Wrapper) {
    gradleVersion = "3.0"
}

subprojects {
  apply plugin: "java"
  apply plugin: "scala"
  apply plugin: "groovy"
  apply plugin: 'signing' // For pushing to Sonatype OSS Staging repository
  apply plugin: 'com.jfrog.bintray'
  apply plugin: 'maven-publish'

  // JRE 1.6 should work
  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  version = projectVersion
  group = projectGroup

  ext {
      versions = [:]
  }

  versions.scalaDist = scalaDist
  versions.scalaStyle = "0.8.0"
  versions.junit = "4.11"

  repositories {
      mavenCentral()
      jcenter()
  }

  dependencies {
      compile gradleApi()
      compile localGroovy()

      compile "org.scalastyle:scalastyle_${versions.scalaDist}:${versions.scalaStyle}"
      testCompile 'junit:junit:${versions.junit}'
      testCompile ('org.spockframework:spock-core:1.0-groovy-2.4') {
          exclude group:'org.codehaus.groovy'
      }
  }

  compileGroovy.dependsOn(compileScala)

  // Required for Sonatype
  task sourcesJar(type: Jar, dependsOn: classes) {
      from sourceSets.main.allSource
      classifier "sources"
      extension "jar"
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
      from javadoc.destinationDir
      classifier "javadoc"
      extension "jar"
  }

  configurations.create("sources")
  configurations.create("javadoc")
  configurations.archives {
      extendsFrom configurations.sources
      extendsFrom configurations.javadoc
  }

  artifacts {
      sources(sourcesJar) {
          type "jar"
      }
      javadoc(javadocJar) {
          type "javadoc"
      }
  }

  publishing {
      publications {
          mavenJava(MavenPublication) {
              from components.java
              groupId = projectGroup

              artifact sourcesJar {
                  classifier "sources"
              }

              artifact javadocJar {
                  classifier "javadoc"
              }
          }
      }
  }

  bintray {

      user = project.hasProperty('bintray_user') ? project.bintray_user : System.getenv('BINTRAY_USER')
      key = project.hasProperty('bintray_key') ? project.bintray_key : System.getenv('BINTRAY_API_KEY')

      publications = ['mavenJava']
      pkg {
          repo = 'maven'
          name = project.name
          desc = 'gradle-scalastyle is a Gradle plugin for running the Scalastyle style checker.'
          licenses = ['Apache-2.0']
          labels = [version]
          desc = 'Gradle plugin for scalaStyle'
          websiteUrl = projectHomepage
          issueTrackerUrl = 'https://github.com/ngbinh/gradle-scalastyle-plugin/issues'
          vcsUrl = 'https://github.com/ngbinh/gradle-scalastyle-plugin.git'
          licenses = ['Apache-2.0']
          publicDownloadNumbers = true
      }
  }
}
